:Class ShowChmHelp
⍝ This class allows you to show up CHM help files from APL.
⍝
⍝ You can also display particular topics, but for this you must know the topic name.
⍝
⍝ Note that for technical reasons erasing the instance let the help file disappear
⍝ immediately. The reason is that the DLL **must** be unloaded properly, otherwise
⍝ chances are that APL will crash. So make sure that the instance lives as long as
⍝ as you want the help file to have a visual appearance. As soon as the instance
⍝ is deleted, regardless how, the destructor might get called - and will get
⍝ called, although the precise schedule remains unknown for principle reasons.
⍝
⍝ This class does not support the depricated "DisplayContext" method.
⍝
⍝ Author: Kai Jaeger\\
⍝ Homepage: <http://github.com/aplteam/ShowChmHelp>

    ⎕IO←1 ⋄ ⎕ml←3

    ∇ r←Version
    ⍝ Returns the version number from `TatinVars` if possible.
    ⍝ See also `History`
      :Access Public shared
      :If 0<##.⎕NC'TatinVars.CONFIG'
          r←(⎕JSON⍠('Dialect' 'JSON5')⊢##.TatinVars.CONFIG).version
      :Else
    ⍝ When a script was not loaded with Tatin, there will be no namespace `TatinVars`.
    ⍝ In such cases we cannot provide a version number, so we return just ''.
          r←''
      :EndIf
    ∇

    ∇ History
      :Access Public shared
      ⍝ * 4.1.0
      ⍝   * "Author" and "Homepage" info updated/corrected
      ⍝ * 4.0.0
      ⍝   * Potentially breaking: `Version` now returns a char vector stemming from the package config file
      ⍝   * Packages updated (development only)
      ⍝   * "Make" improved
      ⍝\\
      ⍝ For information regarding older versions see <https://github.com/aplteam/ShowChmHelp/releases>
    ∇

⍝⍝⍝ Constructor

    ∇ make
      :Access Public Instance
      :Implements Constructor
      InitExternalFns ⍬
      Init ⍬
    ∇

    ∇ Quit
⍝⍝⍝ Destructor
      :Implements Destructor
      :If 0<⎕NC'∆Quit'
          {}∆Quit 0 0 18 0
          {}∆Quit 0 0 29 cookie
      :EndIf
      ⎕EX'_'⎕NL 3
      ⎕EX'HH_'
      ⎕EX'∆'⎕NL 2
    ∇

    ∇ InitExternalFns dummy
      'GetDesktopWindow'⎕NA'I4 user32.C32∣GetDesktopWindow'
      '∆DisplayTopic'⎕NA'I4 hhctrl.ocx.C32∣HtmlHelp',('*A'⊃⍨1+12>{⍎⍵↑⍨¯1+⍵⍳'.'}2⊃'.'⎕WG'APLVersion'),' I <0T[] I I'
      '∆DisplayHelpfile'⎕NA'I4 hhctrl.ocx.C32|HtmlHelp',('*A'⊃⍨1+12>{⍎⍵↑⍨¯1+⍵⍳'.'}2⊃'.'⎕WG'APLVersion'),' I <0T[] I I'
      '∆Quit'⎕NA'I4 hhctrl.ocx.C32|HtmlHelp',('*A'⊃⍨1+12>{⍎⍵↑⍨¯1+⍵⍳'.'}2⊃'.'⎕WG'APLVersion'),' I I I I'
      '∆Init'⎕NA'I4 hhctrl.ocx.C32|HtmlHelp',('*A'⊃⍨1+12>{⍎⍵↑⍨¯1+⍵⍳'.'}2⊃'.'⎕WG'APLVersion'),' I I I >I'
      '∆GetLastError'⎕NA'I4 kernel32.C32|GetLastError'
    ∇

    ∇ Init Parms;Allowed
      _WindowsDefault←''
      :If IsChar _WindowsDefault
          _WindowsDefault←1 ⎕C _WindowsDefault
          :If 0≠≢,_WindowsDefault
          :AndIf (⊂_WindowsDefault)∊'DEVELOPMENT' 'DESKTOP'
              6 ⎕SIGNAL⍨'Check parameter "WindowsDefault"!'
          :EndIf
      :EndIf
      cookie←2⊃∆Init 0 0 28 0
      ⎕EX'HH_'
      CreateHH_Constants
    ∇

    ∇ CheckInit Dummy
      :If 0=⎕NC'∆WINDOWDEFAULT'
          Init''
      :ElseIf 0=⎕NC'HH_'
      :OrIf 0∊⍴HH_.⎕NL 2
          CreateHH_Constants
      :EndIf
    ∇

⍝⍝⍝ Public

    ∇ {R}←DisplayHelpfile Filename;handle
      :Access Public Instance
      handle←GetDefaultHandle''
      :If '.CHM'≢1 ⎕C ¯4↑Filename
          Filename,←'.chm'
      :EndIf
      :If ⎕NEXISTS Filename
          R←(∆DisplayHelpfile handle Filename HH_.DISPLAY_TOPIC 0)''
      :Else
          R←¯1 'File not found'
      :EndIf
    ∇

    ∇ {r}←{cs}DisplayTopic(filename topicName);appHandle;helpWindowName;allowed
 ⍝ Displays a particular topic identified by name within a particular help file.\\
 ⍝ Optional parameters: `HelpWindowName`, `appHandle`.
      :Access Public Instance
      appHandle←GetDefaultHandle''
      helpWindowName←'Main'
      :If 0<⎕NC'cs'
      :AndIf ~0∊⍴,cs
          allowed←'helpWindowName' 'appHandle'
          'Invalid optional parameters'⎕SIGNAL 11/⍨~∧/(cs.⎕NL-2)∊allowed
          ⍎cs.{l←⊃,/' ',¨⎕NL-2 ⋄
              ('(',l,')←cs.⍎''',l,'''')}⍬
      :EndIf
      :If ⎕NEXISTS filename
          :If '.HTM'≢¯4↑filename←filename,'::/',topicName
              filename,←'.htm'
          :EndIf
          :If ~'>'∊filename
          :AndIf ~0∊⍴helpWindowName
              filename,←'>',helpWindowName
          :EndIf
          r←(∆DisplayTopic appHandle filename HH_.DISPLAY_TOPIC 0)''
      :Else
          r←¯1 'File not found'
      :EndIf
    ∇

⍝⍝⍝ Private stuff

    ∇ R←GetDefaultHandle Type
    ⍝ Type may be "Development" or "Desktop" or empty.
    ⍝ If it is empty, _WindowsDefault is taken as a default.
    ⍝ If _WindowsDefault is empty, "Development" is taken as default.
      :If 0∊⍴,Type
          :If ~0∊⍴,_WindowsDefault ⍝ can be specified by "Init"
              Type←_WindowsDefault
          :Else
              Type←'Development'
          :EndIf
      :EndIf
      :If 'Development'≡4⊃'.'⎕WG'aplVersion'
      :AndIf 'DEVELOPMENT'≡Type
          R←'⎕se'⎕WG'Handle'
      :Else
          R←GetDesktopWindow
      :EndIf
    ∇

    ∇ CreateHH_Constants
      ⎕EX'HH_'
      'HH_'⎕NS''
      :With 'HH_'
          DISPLAY_TOPIC←0
          HELP_FINDER←0
          DISPLAY_TOC←1
          DISPLAY_INDEX←2
          DISPLAY_SEARCH←3
          SET_WIN_TYPE←4
          GET_WIN_TYPE←5
          GET_WIN_HANDLE←6
          ENUM_INFO_TYPE←7
          SET_INFO_TYPE←8
          SYNC←9
          RESERVED1←10
          RESERVED2←11
          RESERVED3←12
          KEYWORD_LOOKUP←13
          DISPLAY_TEXT_POPUP←14
          HELP_CONTEXT←15
          TP_HELP_CONTEXTMENU←16
          TP_HELP_WM_HELP←17
          CLOSE_ALL←18
      :End
    ∇

    IsChar←{' '=1↑0⍴∊⍵}

:EndClass
